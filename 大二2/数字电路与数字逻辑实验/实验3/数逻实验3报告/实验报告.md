# 反应速度测试仪

无04  2019012137  张鸿琳

## 一. 实验目的

了解和掌握UART的工作原理，掌握仿真验证方法，为后续设计做准备。



## 二. 实验原理

​		UART（Universal Asynchronous Receiver/Transmitter）是一种通用串行数据总线， 用于异步通信。该总线双向通信，可以实现全双工传输和接收。在嵌入式设计中，UART用来与PC进行通信，包括与监控调试器和其它器件。UART是计算机与嵌入式系统中最常用的串行通信协议，速率有规定的9600等波特率。串口时序示意图如下：

![](C:\Users\惠普\Videos\Captures\实验三__UART_MEM.pdf 和另外 1 个页面 - 个人 - Microsoft​ Edge 2022_5_16 19_51_33.png)

​		发送一个完整的字节信息，首先是一个作为起始位的逻辑“0”位，接着是8个数据位，然后是1个、1＋1/2个或2个停止位逻辑“1”位，数据线空闲时呈现为高或“1”状态。在字符的8位数据部分，先发送数据的最低位（LSB），最后发送最高位（MSB）。每位持续的时间是固定的，由发送器本地时钟控制，每秒发送的数据位个数，即为“波特率”。



## 三. 实验内容

- 阅读Verilog代码，理解电路实现原理。
- 对代码进行行为级验证。
- 建立工程，通过综合实现，将比特流加载到开发板上，对设计进行验证。
- 选做：添加一个信号，当该信号有效时，将数据存储器中的内容取出，逐字取反码，并存回数据存储器中。该信号可绑定在开发板的某个按键上，并通过行为级仿真和开发板上进行实现进行软件和硬件验证。



## 四. 代码解释

#### UART_MEM.v

```verilog
module UART_MEM(
    input clk,      // 100MHz
    input rst,      // S1 
    input mem2uart, // SW0     
    /*---------------------------MEM--------------------------------*/
    output reg recv_done,   // led 0 
    output reg send_done,   // led 1     
    /*---------------------------UART-------------------------------*/
    input Rx_Serial,
    output Tx_Serial
    );
    
    parameter CLKS_PER_BIT = 16'd10417;  // 100M/9600
    parameter MEM_SIZE = 512;
    
    
    /*--------------------------------UART RX-------------------------*/
    wire Rx_DV;
    wire [7:0] Rx_Byte;
    
    uart_rx #(.CLKS_PER_BIT(CLKS_PER_BIT)) uart_rx_inst
        (.i_Clock(clk),
         .i_Rx_Serial(Rx_Serial),
         .o_Rx_DV(Rx_DV),
         .o_Rx_Byte(Rx_Byte)
         );
         
    /*--------------------------------UART TX-------------------------*/
    reg Tx_DV;
    reg [7:0] Tx_Byte;
    wire Tx_Active;
    wire Tx_Done;
    
    //assign Tx_DV = Rx_DV;
    //assign Tx_Byte = Rx_Byte+1'b1;
    
    uart_tx #(.CLKS_PER_BIT(CLKS_PER_BIT)) uart_tx_inst
        (.i_Clock(clk),
         .i_Tx_DV(Tx_DV),
         .i_Tx_Byte(Tx_Byte),
         .o_Tx_Active(Tx_Active),
         .o_Tx_Serial(Tx_Serial),
         .o_Tx_Done(Tx_Done)
         );
         
    /*----------------------------------MEM----------------------------*/
    // instruction memory
    reg [15:0] addr0;
    reg rd_en0;
    reg wr_en0;
    wire [31:0] rdata0;
    reg [31:0] wdata0;
    
    mem #(.MEM_SIZE(MEM_SIZE)) mem0 (
        .clk(clk),  
        .addr(addr0), 
        .rd_en(rd_en0), 
        .wr_en(wr_en0), 
        .rdata(rdata0), 
        .wdata(wdata0)
        );
        
        
    // data memory
    reg [15:0] addr1;
    reg rd_en1;
    reg wr_en1;
    wire [31:0] rdata1;
    reg [31:0] wdata1;
        
    mem #(.MEM_SIZE(MEM_SIZE)) mem1 (
        .clk(clk), 
        .addr(addr1), 
        .rd_en(1'b1), 
        .wr_en(wr_en1), 
        .rdata(rdata1), 
        .wdata(wdata1)
        );
        
    /*----------------------------------MEM Control----------------------------*/
    
    reg [2:0] byte_cnt;
    reg [31:0] word;
    //reg recv_done;
    reg [31:0] cntByteTime;
    //reg send_done;
    
    always@(posedge clk)begin
        if(rst)begin
            addr0 <= 16'd0;
            rd_en0 <= 1'b0;
            wr_en0 <= 1'b0;
            wdata0 <= 32'd0;
            addr1 <= 16'd0;
            rd_en1 <= 1'b0;
            wr_en1 <= 1'b0;
            wdata1 <= 32'd0;
            byte_cnt <= 3'd0;
            word <= 32'd0;
            recv_done <= 1'b0;
            cntByteTime <= 32'd0;
            
            Tx_DV <= 1'b0;
            Tx_Byte <= 8'd0;
            send_done <= 1'b0;
        end
        else
        begin
            // uart to memory
            if(Rx_DV)begin
                // receive a word = 4Byte
                if(byte_cnt == 3'd3)begin
                    byte_cnt <= 3'd0;
                    
                    // receive instruction
                    if(addr0 < MEM_SIZE)begin
                        addr0 <= addr0+1'b1;
                        wr_en0 <= 1'b1;
                        wr_en1 <= 1'b0;
                        wdata0 <= {Rx_Byte,word[23:0]};
                    end
                    // receive data
                    else begin
                        if(addr1 < MEM_SIZE)begin
                            addr1 <= addr1+1'b1;
                            wr_en0 <= 1'b0;
                            wr_en1 <= 1'b1;
                            wdata1 <= {Rx_Byte,word[23:0]};
                            
                        end
                    end
                end
                else begin
                    byte_cnt <= byte_cnt+1'b1;
                    
                    if(byte_cnt==3'd0) word[7:0] <= Rx_Byte;
                    else if(byte_cnt==3'd1) word[15:8] <= Rx_Byte;
                    else if(byte_cnt==3'd2) word[23:16] <= Rx_Byte;
                    else;
                    
                    wr_en0 <= 1'b0;
                    wr_en1 <= 1'b0;
                end
                
                
            end
            else begin
                wr_en0 <= 1'b0;
                wr_en1 <= 1'b0;
                
                if(addr1 == MEM_SIZE && recv_done == 1'b0)begin  // receive done
                    recv_done <= 1'b1;
                    addr1 <= 16'd0;
                    byte_cnt <= 3'd0;
                end
            end
        
            // memory to uart
            if(mem2uart==1'b1)begin
                if(cntByteTime == CLKS_PER_BIT*20 && send_done==1'b0)begin  // 1Byte time
                    cntByteTime <= 32'd0;
                    
                    if(addr1 != 16'd0) Tx_DV <= 1'b1;
                    if(byte_cnt==3'd0) Tx_Byte <= rdata1[7:0];
                    else if(byte_cnt==3'd1) Tx_Byte <= rdata1[15:8];
                    else if(byte_cnt==3'd2) Tx_Byte <= rdata1[23:16];
                    else if(byte_cnt==3'd3) Tx_Byte <= rdata1[31:24];
                    else;
                    
                    if(byte_cnt == 3'd3)begin
                        byte_cnt <= 3'd0;
                        
                        if(addr1 < MEM_SIZE)begin
                            addr1 <= addr1+1'b1;
                        end
                        else begin
                            send_done <= 1'b1;
                        end
                        
                    end
                    else begin
                        byte_cnt <= byte_cnt+1'b1;    
                    end
                    
                end
                else begin
                    cntByteTime <= cntByteTime+1'b1;         
                    Tx_DV <= 1'b0;
                end
            end       
        end
    end
endmodule
```

​		该代码为顶层代码，通过调用下面的一些基础模块：负责发送的uart_rx、负责接收的uart_tx、写入和读出数据的mem，从而实现UART串口通信。

​		rst为复位键，mem2uart表示当前是否可以开始由存储空间向串口发送数据，recv_done表示当前数据接收是否完成（完成则点亮led0），send_done表示当前数据发送是否完成（完成则点亮led1），Rx_Serial为接收数据端，Tx_Serial为发送数据端。

​		参数CLKS_PER_BIT用于衡量波特率，比如当波特率为9600，那么就需要每秒钟发出9600个数据位，而本次实验的时钟频率为$100MHz$，那么利用时钟计数到$100\times10^6/9600\approx10417$时，就需要执行一次数据位操作，所以代码中令CLKS_PER_BIT=10417。

​		在always语句中，当Rx_DV为1时，也就是uart_rx完成了一次数据接收，将其接收到的1byte数据读取到wdata0或wdata1中，当wdata的32位填满后，wdata0将指令依据addr0指向地址存入指令存储空间（512word），而wdata1则将数据依据addr1指向地址存入数据存储空间（512word）。

​		当数据接收完成后，也就是addr1（数据存储空间地址）指向最后一位时，recv_done被拉高，led0随之被点亮，同时addr1被复位，便于此后输出存储的数据。

​		数据接收完毕，mem2uart被置为1时，就会随时钟不断从先前的数据存储空间中通过addr1指针以byte为单位读取数据到Tx_Byte中，此后Tx_DV被拉高，这个byte的数据就通过uart_tx模块发送出去了，每个byte的发送周期是20个CLKS_PER_BIT，保证了充足的缓冲空间。当addr1指向数据存储空间末端时，意味着所有数据已经被发送，则将send_done置为1，led1随之被点亮。

#### uart_rx.v

```verilog
module uart_rx 
  #(parameter CLKS_PER_BIT = 10417)
  (
   input        i_Clock,         
   input        i_Rx_Serial,
   output       o_Rx_DV,
   output [7:0] o_Rx_Byte
   );
    
  parameter s_IDLE         = 3'b000;
  parameter s_RX_START_BIT = 3'b001;
  parameter s_RX_DATA_BITS = 3'b010;
  parameter s_RX_STOP_BIT  = 3'b011;
  parameter s_CLEANUP      = 3'b100;
   
  reg           r_Rx_Data_R = 1'b1;
  reg           r_Rx_Data   = 1'b1;
   
  reg [15:0]     r_Clock_Count = 0;
  reg [2:0]     r_Bit_Index   = 0; //8 bits total
  reg [7:0]     r_Rx_Byte     = 0;
  reg           r_Rx_DV       = 0;
  reg [2:0]     r_SM_Main     = 0;
   
  // Purpose: Double-register the incoming data.
  // This allows it to be used in the UART RX Clock Domain.
  // (It removes problems caused by metastability)
  always @(posedge i_Clock)
    begin
      r_Rx_Data_R <= i_Rx_Serial;
      r_Rx_Data   <= r_Rx_Data_R;
    end
   
   
  // Purpose: Control RX state machine
  always @(posedge i_Clock)
    begin
       
      case (r_SM_Main)
        s_IDLE :
          begin
            r_Rx_DV       <= 1'b0;
            r_Clock_Count <= 0;
            r_Bit_Index   <= 0;
             
            if (r_Rx_Data == 1'b0)          // Start bit detected
              r_SM_Main <= s_RX_START_BIT;
            else
              r_SM_Main <= s_IDLE;
          end
         
        // Check middle of start bit to make sure it's still low
        s_RX_START_BIT :
          begin
            if (r_Clock_Count == (CLKS_PER_BIT-1)/2)
              begin
                if (r_Rx_Data == 1'b0)
                  begin
                    r_Clock_Count <= 0;  // reset counter, found the middle
                    r_SM_Main     <= s_RX_DATA_BITS;
                  end
                else
                  r_SM_Main <= s_IDLE;
              end
            else
              begin
                r_Clock_Count <= r_Clock_Count + 1;
                r_SM_Main     <= s_RX_START_BIT;
              end
          end // case: s_RX_START_BIT
         
         
        // Wait CLKS_PER_BIT-1 clock cycles to sample serial data
        s_RX_DATA_BITS :
          begin
            if (r_Clock_Count < CLKS_PER_BIT-1)
              begin
                r_Clock_Count <= r_Clock_Count + 1;
                r_SM_Main     <= s_RX_DATA_BITS;
              end
            else
              begin
                r_Clock_Count          <= 0;
                r_Rx_Byte[r_Bit_Index] <= r_Rx_Data;
                 
                // Check if we have received all bits
                if (r_Bit_Index < 7)
                  begin
                    r_Bit_Index <= r_Bit_Index + 1;
                    r_SM_Main   <= s_RX_DATA_BITS;
                  end
                else
                  begin
                    r_Bit_Index <= 0;
                    r_SM_Main   <= s_RX_STOP_BIT;
                  end
              end
          end // case: s_RX_DATA_BITS
     
     
        // Receive Stop bit.  Stop bit = 1
        s_RX_STOP_BIT :
          begin
            // Wait CLKS_PER_BIT-1 clock cycles for Stop bit to finish
            if (r_Clock_Count < CLKS_PER_BIT-1)
              begin
                r_Clock_Count <= r_Clock_Count + 1;
                r_SM_Main     <= s_RX_STOP_BIT;
              end
            else
              begin
                r_Rx_DV       <= 1'b1;
                r_Clock_Count <= 0;
                r_SM_Main     <= s_CLEANUP;
              end
          end // case: s_RX_STOP_BIT
     
         
        // Stay here 1 clock
        s_CLEANUP :
          begin
            r_SM_Main <= s_IDLE;
            r_Rx_DV   <= 1'b0;
          end
         
         
        default :
          r_SM_Main <= s_IDLE;
         
      endcase
    end   
   
  assign o_Rx_DV   = r_Rx_DV;
  assign o_Rx_Byte = r_Rx_Byte;
   
endmodule // uart_rx
```

​		这部分代码负责数据的接收，1个起始位，1个停止位，没有校验位，起始位与停止位之间包含8个数据位，当接收完成，o_rx_dv会被拉高为1。代码中“S_XX"的参数代表不同状态，由上至下分别为：空闲、收到起始位、接收数据、收到停止位、复位。

​		r_Rx_Data_R和r_Rx_Data两个寄存器用于传递接收到的数据，这样可以使接收更稳定。第一个always语句，就是把输入i_Rx_Serial中的数据经由r_Rx_Data_R不断传递给r_Rx_Data。而第二个always语句相当于构造了一个有限状态机，r_SM_Main就是当前状态，最开始处于空闲状态s_IDLE，当接收到‘0’比特时，则判定收到了起始位，进入状态s_RX_START_BIT，对起始位进行确认，确认完毕则进入状态s_RX_DATA_BITS，开始不断接收数据。当数据接收计数r_Bit_Index达到7后，也就是已经收到了8个数据位，状态变为s_RX_STOP_BIT，等待停止位结束后，o_Rx_DV被拉高，表示数据接收已经完成，可以对o_Rx_Byte进行读取，再经过1个时钟周期，状态变为s_CLEANUP，进行复位操作，并回到初始空闲状态，重新等待接收数据。其中o_Rx_DV表示完成了一个byte的接收。

#### uart_tx.v

```verilog
module uart_tx 
  #(parameter CLKS_PER_BIT = 10417)
  (
   input       i_Clock,
   input       i_Tx_DV,
   input [7:0] i_Tx_Byte, 
   output      o_Tx_Active,
   output reg  o_Tx_Serial,
   output      o_Tx_Done
   );
  
  parameter s_IDLE         = 3'b000;
  parameter s_TX_START_BIT = 3'b001;
  parameter s_TX_DATA_BITS = 3'b010;
  parameter s_TX_STOP_BIT  = 3'b011;
  parameter s_CLEANUP      = 3'b100;
   
  reg [2:0]    r_SM_Main     = 0;
  reg [15:0]    r_Clock_Count = 0;
  reg [2:0]    r_Bit_Index   = 0;
  reg [7:0]    r_Tx_Data     = 0;
  reg          r_Tx_Done     = 0;
  reg          r_Tx_Active   = 0;
     
  always @(posedge i_Clock)
    begin
       
      case (r_SM_Main)
        s_IDLE :
          begin
            o_Tx_Serial   <= 1'b1;         // Drive Line High for Idle
            r_Tx_Done     <= 1'b0;
            r_Clock_Count <= 0;
            r_Bit_Index   <= 0;
             
            if (i_Tx_DV == 1'b1)
              begin
                r_Tx_Active <= 1'b1;
                r_Tx_Data   <= i_Tx_Byte;
                r_SM_Main   <= s_TX_START_BIT;
              end
            else
              r_SM_Main <= s_IDLE;
          end // case: s_IDLE
         
         
        // Send out Start Bit. Start bit = 0
        s_TX_START_BIT :
          begin
            o_Tx_Serial <= 1'b0;
             
            // Wait CLKS_PER_BIT-1 clock cycles for start bit to finish
            if (r_Clock_Count < CLKS_PER_BIT-1)
              begin
                r_Clock_Count <= r_Clock_Count + 1;
                r_SM_Main     <= s_TX_START_BIT;
              end
            else
              begin
                r_Clock_Count <= 0;
                r_SM_Main     <= s_TX_DATA_BITS;
              end
          end // case: s_TX_START_BIT
         
         
        // Wait CLKS_PER_BIT-1 clock cycles for data bits to finish         
        s_TX_DATA_BITS :
          begin
            o_Tx_Serial <= r_Tx_Data[r_Bit_Index];
             
            if (r_Clock_Count < CLKS_PER_BIT-1)
              begin
                r_Clock_Count <= r_Clock_Count + 1;
                r_SM_Main     <= s_TX_DATA_BITS;
              end
            else
              begin
                r_Clock_Count <= 0;
                 
                // Check if we have sent out all bits
                if (r_Bit_Index < 7)
                  begin
                    r_Bit_Index <= r_Bit_Index + 1;
                    r_SM_Main   <= s_TX_DATA_BITS;
                  end
                else
                  begin
                    r_Bit_Index <= 0;
                    r_SM_Main   <= s_TX_STOP_BIT;
                  end
              end
          end // case: s_TX_DATA_BITS
         
         
        // Send out Stop bit.  Stop bit = 1
        s_TX_STOP_BIT :
          begin
            o_Tx_Serial <= 1'b1;
             
            // Wait CLKS_PER_BIT-1 clock cycles for Stop bit to finish
            if (r_Clock_Count < CLKS_PER_BIT-1)
              begin
                r_Clock_Count <= r_Clock_Count + 1;
                r_SM_Main     <= s_TX_STOP_BIT;
              end
            else
              begin
                r_Tx_Done     <= 1'b1;
                r_Clock_Count <= 0;
                r_SM_Main     <= s_CLEANUP;
                r_Tx_Active   <= 1'b0;
              end
          end // case: s_Tx_STOP_BIT
         
         
        // Stay here 1 clock
        s_CLEANUP :
          begin
            r_Tx_Done <= 1'b1;
            r_SM_Main <= s_IDLE;
          end
         
         
        default :
          r_SM_Main <= s_IDLE;
         
      endcase
    end
 
  assign o_Tx_Active = r_Tx_Active;
  assign o_Tx_Done   = r_Tx_Done;
   
endmodule
```

​		这部分负责数据发送的代码和上面负责接收的代码相对应，不再赘述细节，只是多了一个o_Tx_Active，表示当前正在传输数据，而i_Tx_DV由外部输入，表示当前可以发送i_Tx_Byte中的数据了，o_Tx_Done表示完成了当前byte的输出。

#### mem.v

```verilog
module mem
#(parameter MEM_SIZE = 512)
(
    input clk,
    input [15:0] addr,
    input rd_en,
    input wr_en,
    output reg [31:0] rdata,
    input [31:0] wdata
    );
    
    
    reg [31:0] memData [MEM_SIZE:1];

    
    always@(posedge clk)begin
     begin
            if(wr_en) memData[addr] <= wdata;
            if(rd_en)  rdata <=  memData[addr];
            else rdata<= 32'd0; 
        end
    end

endmodule
```

​		这部分代码是存储空间写入和读取代码，总共分配了MEM_SIZE=512word的空间，当rd_en被拉高时，从存储空间中读取数据到rdata中，当wr_en被拉高时，向存储空间写入数据wdata，而读取和写入的位置由addr地址确定。（当不读取时，输出rdata=0）

#### 约束文件

```verilog
#100MHz
set_property PACKAGE_PIN P17 [get_ports clk]
set_property IOSTANDARD LVCMOS33 [get_ports clk]
create_clock -name clk -period 10.000 [get_ports clk]

#S6
set_property PACKAGE_PIN R17 [get_ports rst]
set_property IOSTANDARD LVCMOS33 [get_ports rst]

#S1
set_property PACKAGE_PIN R1 [get_ports mem2uart]
set_property IOSTANDARD LVCMOS33 [get_ports mem2uart]


#led 0
set_property PACKAGE_PIN K2 [get_ports recv_done]
set_property IOSTANDARD LVCMOS33 [get_ports recv_done]

#led 1
set_property PACKAGE_PIN J2 [get_ports send_done]
set_property IOSTANDARD LVCMOS33 [get_ports send_done]


#UART
set_property PACKAGE_PIN N5 [get_ports Rx_Serial]
set_property PACKAGE_PIN T4 [get_ports Tx_Serial]

set_property IOSTANDARD LVCMOS33 [get_ports Rx_Serial]
set_property IOSTANDARD LVCMOS33 [get_ports Tx_Serial]

#取反
set_property PACKAGE_PIN P5 [get_ports inv]
set_property IOSTANDARD LVCMOS33 [get_ports inv]

create_clock -period 10.000 -name CLK -waveform {0.000 5.000} [get_ports clk]
```

​		P17为FPGA自带时钟，时钟周期为100MHz，R17为复位键，R1被拉高时，则FPGA开始通过串口发送存储空间中的数据，K2灯亮表明接收完毕，J2灯亮表明发送完毕，N5为FPGA的接收串口，T4为FPGA的发送串口。



## 五. 选做部分的实现

​		在编写取反操作的代码时，发现原代码存在一定问题，由于写入存储空间的地址指针只有addr1一个，这就导致该代码实现的串口实际上是半双工的，因为接收和发送存在一定地址冲突（这个操作改写另一个操作的地址），我对它进行了一点修改，使之变成了更稳定的半双工，同时加入了取反操作（这里默认每次reset后最多进行一次取反操作，因为多次取反没有太大意义），代码如下（增加了inv输入，拉高时进行取反操作，代码中的修改部分有注释标注）：

```verilog
module UART_MEM(
    input clk,      // 100MHz
    input rst,      // S1 
    input mem2uart, // SW0     
    /*---------------------------MEM--------------------------------*/
    output reg recv_done,   // led 0 
    output reg send_done,   // led 1     
    /*---------------------------UART-------------------------------*/
    input Rx_Serial,
    output Tx_Serial,
    input inv  //取反
    );
    
    parameter CLKS_PER_BIT = 16'd10417;  // 100M/9600
    parameter MEM_SIZE = 512;
    
    reg [31:0] temp;//用于取反
    reg [1:0] state;//0时取出数据并取反，1时放回数据，2时addr1+1
    reg done;//判断是否取反完成
    reg [31:0] count;//计数
    reg ever;//判断是否接受到过数据
    
    
    /*--------------------------------UART RX-------------------------*/
    wire Rx_DV;
    wire [7:0] Rx_Byte;
    
    uart_rx #(.CLKS_PER_BIT(CLKS_PER_BIT)) uart_rx_inst
        (.i_Clock(clk),
         .i_Rx_Serial(Rx_Serial),
         .o_Rx_DV(Rx_DV),
         .o_Rx_Byte(Rx_Byte)
         );
         
    /*--------------------------------UART TX-------------------------*/
    reg Tx_DV;
    reg [7:0] Tx_Byte;
    wire Tx_Active;
    wire Tx_Done;
    
    //assign Tx_DV = Rx_DV;
    //assign Tx_Byte = Rx_Byte+1'b1;
    
    uart_tx #(.CLKS_PER_BIT(CLKS_PER_BIT)) uart_tx_inst
        (.i_Clock(clk),
         .i_Tx_DV(Tx_DV),
         .i_Tx_Byte(Tx_Byte),
         .o_Tx_Active(Tx_Active),
         .o_Tx_Serial(Tx_Serial),
         .o_Tx_Done(Tx_Done)
         );
         
    /*-----------------------------MEM----------------------------*/
    // instruction memory
    reg [15:0] addr0;
    reg rd_en0;
    reg wr_en0;
    wire [31:0] rdata0;
    reg [31:0] wdata0;
    
    mem #(.MEM_SIZE(MEM_SIZE)) mem0 (
        .clk(clk),  
        .addr(addr0), 
        .rd_en(rd_en0), 
        .wr_en(wr_en0), 
        .rdata(rdata0), 
        .wdata(wdata0)
        );
        
        
    // data memory
    reg [15:0] addr1;
    reg rd_en1;
    reg wr_en1;
    wire [31:0] rdata1;
    reg [31:0] wdata1;
        
    mem #(.MEM_SIZE(MEM_SIZE)) mem1 (
        .clk(clk), 
        .addr(addr1), 
        .rd_en(1'b1), 
        .wr_en(wr_en1), 
        .rdata(rdata1), 
        .wdata(wdata1)
        );
        
    /*-------------------------MEM Control----------------------------*/
    
    reg [2:0] byte_cnt;
    reg [31:0] word;
    //reg recv_done;
    reg [31:0] cntByteTime;
    //reg send_done;
    
    always@(posedge clk)begin
        if(rst)begin
            addr0 <= 16'd0;
            rd_en0 <= 1'b0;
            wr_en0 <= 1'b0;
            wdata0 <= 32'd0;
            addr1 <= 16'd0;
            rd_en1 <= 1'b0;
            wr_en1 <= 1'b0;
            wdata1 <= 32'd0;
            byte_cnt <= 3'd0;
            word <= 32'd0;
            recv_done <= 1'b0;
            cntByteTime <= 32'd0;
            
            Tx_DV <= 1'b0;
            Tx_Byte <= 8'd0;
            send_done <= 1'b0;
            
            state <= 0;
            done <= 0;
            count <= 0;
            temp <= 0;
            ever <= 0;
        end
        else
        begin
            // uart to memory
            if(Rx_DV)begin
                // receive a word = 4Byte
                ever <= 1;
                if(byte_cnt == 3'd3)begin
                    byte_cnt <= 3'd0;
                    
                    // receive instruction
                    if(addr0 < MEM_SIZE)begin
                        addr0 <= addr0+1'b1;
                        wr_en0 <= 1'b1;
                        wr_en1 <= 1'b0;
                        wdata0 <= {Rx_Byte,word[23:0]};
                    end
                    // receive data
                    else begin
                        if(addr1 < MEM_SIZE)begin
                            addr1 <= addr1+1'b1;
                            wr_en0 <= 1'b0;
                            wr_en1 <= 1'b1;
                            wdata1 <= {Rx_Byte,word[23:0]};
                            
                        end
                    end
                end
                else begin
                    byte_cnt <= byte_cnt+1'b1;
                    
                    if(byte_cnt==3'd0) word[7:0] <= Rx_Byte;
                    else if(byte_cnt==3'd1) word[15:8] <= Rx_Byte;
                    else if(byte_cnt==3'd2) word[23:16] <= Rx_Byte;
                    else;
                    
                    wr_en0 <= 1'b0;
                    wr_en1 <= 1'b0;
                end
            end
            else begin
            
                //以下为选做的取反部分
                if(inv == 1)begin
                    if((done == 0) && (addr1 <= MEM_SIZE))begin
                        if(addr1 == 0)addr1 = 16'b1;//初始值设为1
                        else;
                        if(count == CLKS_PER_BIT)begin//操作周期
                            count <= 0;
                            if(state == 0)begin//取出数据并取反
                                wr_en1 <= 1'b0;
                                temp <= ~rdata1;
                                state <= 1;
                            end
                            else begin//放回数据
                                if(state == 1)begin
                                    state <= 2;
                                    wdata1 <= temp;
                                    wr_en1 <= 1'b1;
                                end
                                else begin
                                    addr1 <= addr1 + 1'b1;
                                    wr_en1 <= 1'b0;
                                    state <= 0;
                                end
                            end
                        end
                        else count <= count+1;
                    end
                    else begin
                        wr_en1 <= 1'b0;
                        addr1 <= 16'd0;
                        done <= 1;
                        state <= 0;
                    end
                end
                
                else begin//原代码
                    wr_en0 <= 1'b0;
                    wr_en1 <= 1'b0;
                
        //增加一个判别条件，发送时不写入，使得该代码实现的串口变成半双工的但更稳定
                    if(ever==1 && mem2uart == 1'b0)begin
                        // receive done
                        if(addr1 == MEM_SIZE && recv_done == 1'b0)begin  
                            recv_done <= 1'b1;
                            addr1 <= 16'd0;
                            byte_cnt <= 3'd0;
                        end
                    end
                end
            end
        
            // memory to uart
            if(mem2uart==1'b1)begin
                // 1Byte time
                if(cntByteTime == CLKS_PER_BIT*20 && send_done==1'b0)begin  
                    cntByteTime <= 32'd0;
                    
                    if(addr1 != 16'd0) Tx_DV <= 1'b1;
                    if(byte_cnt==3'd0) Tx_Byte <= rdata1[7:0];
                    else if(byte_cnt==3'd1) Tx_Byte <= rdata1[15:8];
                    else if(byte_cnt==3'd2) Tx_Byte <= rdata1[23:16];
                    else if(byte_cnt==3'd3) Tx_Byte <= rdata1[31:24];
                    else;
                    
                    if(byte_cnt == 3'd3)begin
                        byte_cnt <= 3'd0;
                        
                        if(addr1 < MEM_SIZE)begin
                            addr1 <= addr1+1'b1;
                        end
                        else begin
                            send_done <= 1'b1;
                        end
                        
                    end
                    else begin
                        byte_cnt <= byte_cnt+1'b1;    
                    end
                    
                end
                else begin
                    cntByteTime <= cntByteTime+1'b1;         
                    Tx_DV <= 1'b0;
                end
            end       
        end    
    end

endmodule
```



## 六. 仿真结果及分析

​		仿真代码如下（该仿真验证了添加选做内容后的代码的正确性）：

```verilog
`timescale 1ps / 1ps
`define PERIOD 10    //对应时钟周期为100MHz

module UART_MEM_tb();

reg clk;
reg rst;
reg mem2uart;
reg inv;//判断是否取反
    
//存需要发出的数据，11111111_00000000_10101010_10110010，发送两遍
reg [31:0] info=32'hff00aab2;

reg tx_dv;
reg [7:0] tx_byte;
reg [31:0] cntByteTime;//用于衡量数据发送时间
reg [2:0] byte_cnt;
reg info_done;

parameter CLKS_PER_BIT=32'd10417;

wire r_done;
wire t_done;
wire rx;
wire tx;

UART_MEM #(.MEM_SIZE(1)) uart_mem//定义1个word的空间
(
    .clk(clk),      // 100MHz
    .rst(rst),      // S1 
    .mem2uart(mem2uart), // SW0     
    .recv_done(r_done),   // led 0 
    .send_done(t_done),   // led 1     
    .Rx_Serial(rx),
    .Tx_Serial(tx),
    .inv(inv)  //取反
);

uart_tx sendinfo//发送info中信息
  (
   .i_Clock(clk),
   .i_Tx_DV(tx_dv),
   .i_Tx_Byte(tx_byte),
   .o_Tx_Serial(rx)
   );

initial begin
    rst=0;
    mem2uart=0;
    clk=0;
    inv=0;
    mem2uart=0;
    tx_dv=0;
    tx_byte=0;
    cntByteTime=0;
    byte_cnt=0;
    info_done=1;
    #(800)rst=1;//进行初始赋值
    #(200)rst=0;
    #(1000)info_done=0;//开始发送info中数据
    #(CLKS_PER_BIT*2000)mem2uart=1;//开始输出存储的信息
    #(CLKS_PER_BIT*2000)mem2uart=0;
    #(1000)rst=1;//复位，可以重新发送数据
    #(1000)rst=0;
    #(1000)inv=1;//取反
    #(CLKS_PER_BIT*50)inv=0;
    #(1000)mem2uart=1;//取反完成，发送取反后的数据
end

initial begin//生成100MHz时钟
    forever
      #(`PERIOD/2) clk=~clk;
end

always @(posedge clk)//发送info信息
begin
    if(cntByteTime == CLKS_PER_BIT*20 && info_done==0)begin  // 1Byte time
        cntByteTime <= 32'd0;
        tx_dv <= 1'b1;
        
        if(byte_cnt==3'd0) tx_byte <= info[7:0];
        else if(byte_cnt==3'd1) tx_byte <= info[15:8];
        else if(byte_cnt==3'd2) tx_byte <= info[23:16];
        else if(byte_cnt==3'd3) tx_byte <= info[31:24];
        else if(byte_cnt==3'd4) tx_byte <= info[7:0];
        else if(byte_cnt==3'd5) tx_byte <= info[15:8];
        else if(byte_cnt==3'd6) tx_byte <= info[23:16];
        else if(byte_cnt==3'd7)begin
            tx_byte <= info[31:24];
            info_done<=1;
        end
        else;

        if(byte_cnt == 3'd7)begin
            byte_cnt <= 3'd0;
        end
        else begin
            byte_cnt <= byte_cnt+1'b1;  
        end
    end
    else begin
        cntByteTime <= cntByteTime+1'b1;
        tx_dv <= 1'b0;
    end
end

endmodule
```

​		得到的整体仿真图像如下：

![project_7 - [D__projects_project_7_project_7.xpr] - Vivado 2017.3 2022_5_20 11_13_37](C:\Users\惠普\Videos\Captures\project_7 - [D__projects_project_7_project_7.xpr] - Vivado 2017.3 2022_5_20 11_13_37.png)

​		下面具体分析其中细节，首先查看信息接收是否正常，已知发送的序列为11111111_00000000_10101010_10110010，被发送两遍，而rx图像如下：

![project_7 - [D__projects_project_7_project_7.xpr] - Vivado 2017.3 2022_5_20 11_17_23](C:\Users\惠普\Videos\Captures\project_7 - [D__projects_project_7_project_7.xpr] - Vivado 2017.3 2022_5_20 11_17_23.png)

​		可以看到除去起始位0，可以看到传送的序列为01001101_01010101_00000000_11111111，也就是info序列的倒序，并且被发送了两遍，接收完毕后，可以看到r_done被拉高，小灯亮起。

​		下面再检测发送是否正确，如下图：

![project_7 - [D__projects_project_7_project_7.xpr] - Vivado 2017.3 2022_5_20 11_19_08](C:\Users\惠普\Videos\Captures\project_7 - [D__projects_project_7_project_7.xpr] - Vivado 2017.3 2022_5_20 11_19_08.png)

​		在mem2uart被置为高位一段时间后，能看到tx端开始连续发送数据，且可以读出该序列是01001101_01010101_00000000_11111111，也就是刚才所发送的info序列，所以发送也是正确的，并且发送完毕后，t_done被拉高，小灯亮起。

​		最后检查取反操作是否正确，在接收到info信息后，将inv置为1，此后便开始取反操作，取反完成后将存储空间中数据发送出来，如下图：

![project_7 - [D__projects_project_7_project_7.xpr] - Vivado 2017.3 2022_5_20 11_20_12](C:\Users\惠普\Videos\Captures\project_7 - [D__projects_project_7_project_7.xpr] - Vivado 2017.3 2022_5_20 11_20_12.png)

​		可以看到取反后发送出的数据序列为10110010_10101010_11111111_00000000，证明取反操作成功。

## 七. 综合情况

### 面积情况

![project_7 - [D__projects_project_7_project_7.xpr] - Vivado 2017.3 2022_5_19 15_52_10](C:\Users\惠普\Videos\Captures\project_7 - [D__projects_project_7_project_7.xpr] - Vivado 2017.3 2022_5_19 15_52_10.png)

### 时序性能

![project_7 - [D__projects_project_7_project_7.xpr] - Vivado 2017.3 2022_5_19 15_51_27](C:\Users\惠普\Videos\Captures\project_7 - [D__projects_project_7_project_7.xpr] - Vivado 2017.3 2022_5_19 15_51_27.png)

可见WNS与WHS均大于零，故而满足要求。

## 八. 硬件调试情况

​		仿真验证后，将代码烧录到FPGA板上进行验证，首先发送 512 * 2 word 的数据到板子上，然后让板子输出存储的后半部分数据（非指令部分），得到下图：

![友善串口调试助手 2022_5_19 12_31_34](C:\Users\惠普\Videos\Captures\友善串口调试助手 2022_5_19 12_31_34.png)

​		收到板子传来的数据和之前发送的数据一致，说明uart串口功能正常。下面再测试取反操作是否实现，复位后，重新传输数据到板子上（否则取反的就是原来存储的数据，不过此处没有区别，因为复位不擦除原有数据，而再发送的数据也和原有数据一样），然后将取反开关打开一段时间再关闭，让板子发送数据到串口，得到下图：

![友善串口调试助手 2022_5_19 12_32_43](C:\Users\惠普\Videos\Captures\友善串口调试助手 2022_5_19 12_32_43.png)

​		和上一张图片中收到的数据做对比，可以看到原来第一个byte为0x3B，转换为2进制为0011_1011，而取反后得到第一个byte为0xC4，转换为2进制为1100_0100，正确。再看最后一个byte原来为0xFF，转换为2进制为1111_1111，而取反后得到最后一个byte为0x00，转换为2进制为0000_0000，也正确。其他的数据也可以这样，验证基本确认取反操作成功实现。

​		接收和发送数据完成后，小灯正常亮起，如下图：

![7496e1b042c3684ad91950abe7c3eb7](C:\Users\惠普\Desktop\资料\数逻实验\实验3\数逻实验3报告\7496e1b042c3684ad91950abe7c3eb7.jpg)

## 九. 实验小结与文件清单

​		本次实验主要为验证性质的实验，比较简单，不过验证时发现所给代码有一点问题，比如如果一开始没有输入数据，直接输出数据的话，会输出 4092 byte 的数据，经过检查，发现是因为写入数据和输出数据在共同使用一个地址指针 addr1 ，这导致在全双工运行时，二者可能会同时改变 addr1 的值，就会出现错误，而不输入任何数据就发送会输出 4092 byte 也是这个地方出的问题。

​		所以在这次实验的选做部分中，我对代码进行了修改，让其发送时不会写入，实质上使得该代码变成了更稳定的半双工，从而解决了上述问题，对本次实验结果也没有影响，因为本次实验先写入数据，再发送数据（或者取反后再发送），其实没有用到全双工。

​		如果要使代码更加完善，变成真正的全双工，我有两个修改方案：

- 修改 mem.v 中的代码，增加一个地址指针，使得写入与输出不使用同一个指针，不过这样仍有在极短时间内对同一个存储空间进行输入和读取的可能，这时或许是不稳定的。
- 增加一块存储空间，这样一个空间A只写入数据，另一个空间B只输出数据，同时按照固定周期（且没有进行输入与输出时），将空间A中的数据搬运到空间B，这样就可以避免冲突，只是当空间比较大时，每次刷新B的耗时较长。



### 文件清单

- 仿真文件：UART_MEM_tb.v
- 添加取反操作和修改后的顶层文件：UART_MEM.v
- 增加了取反内容的约束文件：UART_MEM.xdc
